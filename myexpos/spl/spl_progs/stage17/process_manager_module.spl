alias functionNum R1;
alias currentPID R2;

// Free User Area Page
if(functionNum == 2) then
	// breakpoint;

	alias userAreaPageNumber R3;
	userAreaPageNumber = [PROCESS_TABLE+(currentPID*16)+11];
	
	// We need to release this page
	multipush(R1,R2,R3);
	R1 = 2;									// Function Number
	R2 = userAreaPageNumber;				// Page Number
	call MEMORY_MANAGER;
	multipop(R1,R2,R3);
	
	// breakpoint;
	return;
	
endif;


// Exit process
if(functionNum == 3) then
	
	// Invoking Free Page table function
	multipush(R1,R2);
	R1 = 4;								// Function Number
	R2 = currentPID;						// PID of current Process
	call PROCESS_MANAGER;
	multipop(R1,R2);
	
	// Invoking Free user area page
	multipush(R1,R2);
	R1 = 2;								// Function Number
	R2 = currentPID;						// PID of current Process
	call PROCESS_MANAGER;
	multipop(R1,R2);
	
	// Set the state of the process at Terminated
	[PROCESS_TABLE+(currentPID*16)+4] = TERMINATED;
	
	return;
	
endif;

// Free Page Table
if(functionNum == 4) then
	
	// Invalidate the page table entries for library page
	[PTBR+0] = -1;
	[PTBR+1] = "0000";
	[PTBR+2] = -1;
	[PTBR+3] = "0000";
	
	// breakpoint;
	
	alias count R3;
	alias pageNum R4;
	count=2;
	while(count < PTLR) do
		pageNum = [PTBR+(2*count)];
		if(pageNum!=-1) then
			multipush(R1,R2,R3,R4);
			R1 = 2;
			R2 = pageNum;
			call MEMORY_MANAGER;
			multipop(R1,R2,R3,R4);
			
			// Invalidate the page table entry
			[PTBR+(2*count)] = -1;
			[PTBR+(2*count)+1] = "0000";
		endif;
		count = count + 1;
	endwhile;
	// breakpoint;
	
	return;
	
endif;
