// Changing the SP from user stack to kernel stack
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13] = SP;
SP = [(PROCESS_TABLE+[SYSTEM_STATUS_TABLE+(5*CORE)+1]*16)+11]*512 - 1;

R1 = ACQUIRE_KERN_LOCK;
call ACCESS_CONTROL;

alias userSP R1;
userSP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];

alias syscall R2;
syscall = [([PTBR+2*((userSP-5)/512)]*512)+((userSP-5)%512)];
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = syscall;


// wait
if(syscall == 13) then

	// print("Wait");
	alias childPid R3;
	childPid = [([PTBR+2*((userSP-4)/512)]*512)+((userSP-4)%512)];
	
	// print(childPid);
	
	// If the childPid value is invalid.
	if(([SYSTEM_STATUS_TABLE+(5*CORE)+1]==childPid)||([PROCESS_TABLE+(childPid*16)+4]==TERMINATED)||(childPid>15)||(childPid<1)) then
		[([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512)] = -1;
		[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = 0;
		
		R1 = RELEASE_LOCK;
		R2 = KERN_LOCK;
		call ACCESS_CONTROL;
		
		SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];
		ireturn;
	endif;
	
	[PROCESS_TABLE+([SYSTEM_STATUS_TABLE+(5*CORE)+1]*16)+4] = WAIT_PROCESS;
	[PROCESS_TABLE+([SYSTEM_STATUS_TABLE+(5*CORE)+1]*16)+5] = childPid;
	
	// breakpoint;
	
	multipush(R1,R2,R3);	
	// Releasing Kernel Lock
	R1 = RELEASE_LOCK;
	R2 = KERN_LOCK;
	call ACCESS_CONTROL;
	
	call SCHEDULER;
	
	// Acquiring Kernel Lock
	R1 = ACQUIRE_KERN_LOCK;
	call ACCESS_CONTROL;
	multipop(R1,R2,R3);
	
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = 0;
	[([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512)] = 0;
	
	R1 = RELEASE_LOCK;
	R2 = KERN_LOCK;
	call ACCESS_CONTROL;
	
	SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];
	
	ireturn;
	
endif;

// signal
if(syscall == 14) then
	
	// print("In here");
	alias count R3;
	count = 1;
	while(count < 16) do
		if(([PROCESS_TABLE+(16*count)+4] == WAIT_PROCESS)&&([PROCESS_TABLE+(16*count)+5]==[SYSTEM_STATUS_TABLE+(5*CORE)+1])) then
			[PROCESS_TABLE+(16*count)+4] = READY;
		endif;
		count = count + 1;
	endwhile;
	
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = 0;
	[([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512)] = 0;
	
	R1 = RELEASE_LOCK;
	R2 = KERN_LOCK;
	call ACCESS_CONTROL;
	
	SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];
	ireturn;
		
endif;

// Getpid
if(syscall == 11) then
	
	[([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512)] = [SYSTEM_STATUS_TABLE+(5*CORE)+1];
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = 0;
	
	R1 = RELEASE_LOCK;
	R2 = KERN_LOCK;
	call ACCESS_CONTROL;
	
	SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];
	
	ireturn;
	
endif;

// Getppid
if(syscall == 12) then

	[([PTBR+2*((userSP-1)/512)]*512)+((userSP-1)%512)] = [PROCESS_TABLE+([SYSTEM_STATUS_TABLE+(5*CORE)+1]*16)+2];
	[PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1]*16 + 9] = 0;
	
	R1 = RELEASE_LOCK;
	R2 = KERN_LOCK;
	call ACCESS_CONTROL;
	
	SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE+(5*CORE)+1] * 16 + 13];
	
	ireturn;
	
endif;
